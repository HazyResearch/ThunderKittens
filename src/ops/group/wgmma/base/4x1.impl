template<int trans_a, int trans_b>
struct wgmma_base<1, trans_a, trans_b> {
    __device__ static inline void rt_st(
        rt_fl<1, 1, ducks::rt_layout::row> &dst,
        const rt_base_bf<ducks::rt_layout::row> & a_rt,
        const uint64_t b_st_desc,
        int scale_d = 1
    ) {
        asm volatile (
            "{\n"
            ".reg .pred p;\n" \
            "setp.ne.b32 p, %13, 0;\n" \
            "wgmma.mma_async.sync.aligned.m64n16k16.f32.bf16.bf16 " \
            "{%0, %1, %2, %3, %4, %5, %6, %7}, " \
            "{%8, %9, %10, %11}, " \
            "%12, " \
            "p, 1, 1, %14;\n" \
            "}\n"
            // a_regs, b_mat descriptor, scale-d, imm-scale-a, imm-scale-b, im-trans-b

        :   "+f"(dst.tiles[0][0].data[0].x), "+f"(dst.tiles[0][0].data[0].y),
            "+f"(dst.tiles[0][0].data[1].x), "+f"(dst.tiles[0][0].data[1].y),
            "+f"(dst.tiles[0][0].data[2].x), "+f"(dst.tiles[0][0].data[2].y),
            "+f"(dst.tiles[0][0].data[3].x), "+f"(dst.tiles[0][0].data[3].y)

        :   "r"(*(uint32_t*)&a_rt.data[0]), "r"(*(uint32_t*)&a_rt.data[1]),
            "r"(*(uint32_t*)&a_rt.data[2]), "r"(*(uint32_t*)&a_rt.data[3]),
            
            "l"(b_st_desc), "r"(scale_d), "n"(trans_b)
        );
    }
    __device__ static inline void st_st(
        rt_fl<1, 1, ducks::rt_layout::row> &dst,
        const uint64_t a_st_desc,
        const uint64_t b_st_desc,
        int scale_d = 1
    ) {
        asm volatile (
            "{\n"
            ".reg .pred p;\n" \
            "setp.ne.b32 p, %10, 0;\n" \
            "wgmma.mma_async.sync.aligned.m64n16k16.f32.bf16.bf16 " \
            "{%0, %1, %2, %3, %4, %5, %6, %7}, " \
            "%8, " \
            "%9, " \
            "p, 1, 1, %11, %12;\n" \
            "}\n"
            // a_mat descriptor, b_mat descriptor, scale-d, imm-scale-a, imm-scale-b, im-trans-a, imm-trans-b

        :   "+f"(dst.tiles[0][0].data[0].x), "+f"(dst.tiles[0][0].data[0].y),
            "+f"(dst.tiles[0][0].data[1].x), "+f"(dst.tiles[0][0].data[1].y),
            "+f"(dst.tiles[0][0].data[2].x), "+f"(dst.tiles[0][0].data[2].y),
            "+f"(dst.tiles[0][0].data[3].x), "+f"(dst.tiles[0][0].data[3].y)

        :   "l"(a_st_desc),
            "l"(b_st_desc),
        
            "r"(scale_d),
            "n"(trans_a),
            "n"(trans_b)
        );
    }
    template<ducks::st::all ST> __device__ static inline uint64_t a_desc(const ST &tile, int chunk_idx) {
        return make_descriptor<trans_a>(tile, chunk_idx);
    }
    template<ducks::st::all ST> __device__ static inline uint64_t b_desc(const ST &tile, int chunk_idx) {
        return make_descriptor<trans_b>(tile, chunk_idx);
    }
};